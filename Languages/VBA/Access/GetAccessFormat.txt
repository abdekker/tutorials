Private Function IsEmpty(strValue As String) As Boolean
    ' Test whether a string is empty (spaces are counted as content, so " " returns FALSE)
    'IsEmpty = (Len(strValue) = 0)
    IsEmpty = (strValue = vbNullString)
End Function

Private Function HasContent(strValue As String) As Boolean
    ' Test a string for content (spaces are not content in this version, so " " == "")
    'HasContent = (Len(Trim(strValue)) = 0)
    HasContent = (Trim(strValue & vbNullString) <> vbNullString)
End Function

Private Function GetDbName() As String
    ' Get the name of the database
    GetDbName = vbNullString
    Dim dbName As String
    dbName = Selection.Value
    If (Not HasContent(dbName)) Then
        ' When no file is provided, hard-code a path here
        'dbName = "P:\HqVar\_clin\Data\clindb1.mdb"
        'dbName = "C:\Apps\Data\Northwind 97.mdb"
        'dbName = "C:\Apps\Data\Northwind 2000.mdb"
        dbName = "C:\Apps\Data\Northwind 2007-2016.accdb"
        'dbName = "C:\Apps\Data\SampleAccess2007.accdb"
    End If
    
    If IsEmpty(Dir(dbName)) Then
        ' File does not exist
        MsgBox "Could not find: " & dbName
        Exit Function
    End If
    GetDbName = dbName
End Function

Sub GetAccessFormat1()
    ' Version 1: Determine the format of an Access database by peeking at the 21st byte of the file header
    ' This method supplied by user "Gord Thompson" on StackOverflow
    On Error GoTo Error_GetAccessFormat1
    Dim dbName As String
    dbName = GetDbName()
    If (IsEmpty(dbName)) Then
        Exit Sub
    End If
    
    ' Read the first twenty-one bytes
    Dim buffer() As Byte
    Dim fileData As Integer
    fileData = FreeFile
    Open dbName For Binary Access Read As fileData
    If (LOF(fileData) > 25) Then
        ReDim buffer(0 To 22)
        Get fileData, , buffer
        Close fileData
    Else
        Exit Sub
    End If
    
    ' Get the file format (byte 21)
    Dim byFileType As Byte
    byFileType = buffer(20) ' buffer is 0-based
    
    ' Convert the file format into a string
    Dim strFormat As String
    Select Case byFileType
        Case 0:
            strFormat = "Access 97 or older"
        Case 1:
            strFormat = "Access 2000/2003"
        Case 2:
            strFormat = "Access 2007"
        Case 3:
            strFormat = "Access 2010"
        Case 5:
            strFormat = "Access 2016 with BIGINT support (Type 5)"
        Case Else:
            strFormat = "Unknown Access file format"
    End Select

    strFormat = (strFormat + " (Type: " & byFileType & ")")
    MsgBox strFormat
    Exit Sub

Error_GetAccessFormat1:
    ' Unable to open the database (not Access or not supported by this version of Office)
    MsgBox "Unable to open as an Access database: " & dbName & ", Error: " & Err.Description
End Sub

Sub GetAccessFormat2()
    ' Version 2: Determine the format of an Access database by using DAO
    ' This method supplied by user "Erik A" on StackOverflow
    ' Note: DAO.DbEngine.36 may not be registered on your system, so this may fail for .mdb files
    On Error GoTo Error_GetAccessFormat2
    Dim dbName As String
    dbName = GetDbName()
    If (IsEmpty(dbName)) Then
        Exit Sub
    End If
    
    ' Open the database using DAO
    If (dbName Like "*.mdb") Then
        ' Old JET engine (32-bit only); requires MDAC version that still supports Access 2.0
        ' Following may fail on 64-bit Excel/VBA. Try adding a reference to "Microsoft DAO 3.6 Object Library"
        Set dbe = CreateObject("DAO.DbEngine.36")
    Else
        ' ACE engine used for modern Access databases
        Set dbe = CreateObject("DAO.DbEngine.120")
    End If
    
    Dim db As Object
    Set db = dbe.OpenDatabase(dbName)
    
    ' Get the file format
    Dim dFileFormat As Double
    dFileFormat = db.Version
    
    ' Convert the file format into a string
    Dim strAccessFormat As String
    If (dFileFormat = 3#) Then
        strAccessFormat = "Microsoft Access 97 or older"
    ElseIf (dFileFormat = 4#) Then
        strAccessFormat = "Microsoft Access 2000-2003"
    ElseIf (dFileFormat = 12#) Then
        strAccessFormat = "Microsoft Access 2007"
    ElseIf (dFileFormat = 14#) Then
        strAccessFormat = "Microsoft Access 2010"
    Else
        strAccessFormat = "Unknown Access file format"
    End If

    strAccessFormat = (strAccessFormat + " (Version: " & dFileFormat & ".0)")
    MsgBox strAccessFormat
    Exit Sub

Error_GetAccessFormat2:
    MsgBox "Unable to open as an Access database: " & dbName & ", Error: " & Err.Description
End Sub

Public Sub GetAccessFormat3()
    ' Version 3: Determine the format of an Access database by running the installed Access application
    ' Note: Access 16.0 (Office 365) cannot fully read databases from Office 2013 (and earlier), and will refuse
    ' to open Access 97 (and earlier) databases. This method is also slow, so use the other methods.
    On Error GoTo Error_GetAccessFormat3
    Dim dbName As String
    dbName = GetDbName()
    If (IsEmpty(dbName)) Then
        Exit Sub
    End If
    
    ' Open the database
    ' Note: There are two methods to create an object of type "Access.Application"
    ' Method 1 (early binding)
    '       Dim objAccess As Access.Application
    '       Set objAccess = New Access.Application
    ' Method 2 (late binding)
    '       Dim objAccess As Object
    '       Set objAccess = CreateObject("Access.Application")
    ' Early binding has the advantage of allowing Intellisense to work
    Dim objAccess As Object
    Set objAccess = CreateObject("Access.Application")
    objAccess.OpenCurrentDatabase dbName
    objAccess.Visible = False
    
    ' Get the file format and close the database
    Dim nFileFormat As Integer
    nFileFormat = objAccess.CurrentProject.FileFormat
    objAccess.CloseCurrentDatabase
    
    ' Convert the file format into a string
    Dim strAccessFormat As String
    Select Case nFileFormat
        Case 2
            strAccessFormat = "Microsoft Access 2"
        Case 7
            strAccessFormat = "Microsoft Access 95"
        Case 8
            strAccessFormat = "Microsoft Access 97"
        Case 9
            strAccessFormat = "Microsoft Access 2000"
        Case 10
            strAccessFormat = "Microsoft Access 2002"
        Case 11
            strAccessFormat = "Microsoft Access 2003"
        Case 12
            strAccessFormat = "Microsoft Access 2007"
        Case 14
            strAccessFormat = "Microsoft Access 2010"
        Case 15
            strAccessFormat = "Microsoft Access 2013"
        Case 16
            strAccessFormat = "Microsoft Access 2016/9"
        Case Else
            strAccessFormat = "Unknown Access file format"
    End Select

    strAccessFormat = (strAccessFormat + " (Type: " & nFileFormat & ")")
    MsgBox strAccessFormat
    Exit Sub

Error_GetAccessFormat3:
    ' Unable to open the database (not Access or not supported by this version of Office)
    MsgBox "Unable to open as an Access database: " & dbName & ", Error: " & Err.Description
End Sub
